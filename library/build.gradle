apply plugin: 'com.android.library'

def commitSHA1 = 'COMMIT_SHA1'

static def getGitCommitCount() {
    def process = "git rev-list HEAD --count".execute()
    return process.text.toInteger()
}

static def getLatestGitHash() {
    def process = "git rev-parse --short HEAD".execute()
    return process.text.toString().trim()
}

static def getVersion() {
    def process = "date +%Y-%m-%d".execute()
    return process.text.toString().trim()
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode getGitCommitCount()
        versionName getVersion() + "." + versionCode + "." + getLatestGitHash()
        buildConfigField "String", commitSHA1, "\"" + getLatestGitHash() + "\""
    }
    buildTypes {
        release {
            minifyEnabled false
            debuggable false
        }

        debug {
            minifyEnabled false
            debuggable true
        }
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.1.0-alpha05"
    implementation("com.google.android.material:material:1.1.0-alpha07") {
        exclude group: 'com.google.guava', module: 'guava'
    }
    // must add guava as top level dependency to force material to use latest version
    implementation 'com.google.guava:guava:27.0.1-android'
}
